# ==================================================== #
# ==================== VPC MODULE ==================== #
# ==================================================== #

# VPC (Virtual Private Cloud)
resource "aws_vpc" "main" {
  cidr_block       = var.vpc_configuration.cidr
  instance_tenancy = "default"

  tags = {
    Name        = "devops-project-vpc"
    Environment = var.environment
    Project     = "devops-project"
    ManagedBy   = "terraform"
  }
}

# ===================== SUBNETS ====================== #

# Public Subnet #1 - WEB
resource "aws_subnet" "subnet_web" {
  vpc_id                  = aws_vpc.main.id
  cidr_block              = var.vpc_configuration.subnets.web.cidr_block
  map_public_ip_on_launch = true
  availability_zone       = var.vpc_configuration.subnets.web.az

  tags = {
    Name        = "subnet-web"
    Environment = var.environment
    Project     = "devops-project"
    ManagedBy   = "terraform"
    Type        = "public"
    Tier        = "web"
  }
}

# Public Subnet #2 - ALB
resource "aws_subnet" "subnet_alb" {
  vpc_id                  = aws_vpc.main.id
  cidr_block              = var.vpc_configuration.subnets.alb.cidr_block
  map_public_ip_on_launch = true
  availability_zone       = var.vpc_configuration.subnets.alb.az

  tags = {
    Name        = "subnet-alb"
    Environment = var.environment
    Project     = "devops-project"
    ManagedBy   = "terraform"
    Type        = "public"
    Tier        = "alb"
  }
}

# Private Subnet #3 - API
resource "aws_subnet" "subnet_api" {
  vpc_id            = aws_vpc.main.id
  cidr_block        = var.vpc_configuration.subnets.api.cidr_block
  availability_zone = var.vpc_configuration.subnets.api.az

  tags = {
    Name        = "subnet-api"
    Environment = var.environment
    Project     = "devops-project"
    ManagedBy   = "terraform"
    Type        = "private"
    Tier        = "api"
  }
}

# Private Subnet #4 - DB
resource "aws_subnet" "subnet_db" {
  vpc_id            = aws_vpc.main.id
  cidr_block        = var.vpc_configuration.subnets.db.cidr_block
  availability_zone = var.vpc_configuration.subnets.db.az

  tags = {
    Name        = "subnet-db"
    Environment = var.environment
    Project     = "devops-project"
    ManagedBy   = "terraform"
    Type        = "private"
    Tier        = "database"
  }
}

# ========== INTERNET GATEWAY & ROUTE TABLE ========== #

# IGW (Internet Gateway)
resource "aws_internet_gateway" "igw" {
  vpc_id = aws_vpc.main.id

  tags = {
    Name        = "devops-project-igw"
    Environment = var.environment
    Project     = "devops-project"
    ManagedBy   = "terraform"
  }
}

# Route Table - Attach IGW to Public Subnets
resource "aws_route_table" "public_rt" {
  vpc_id = aws_vpc.main.id

  route {
    cidr_block = "0.0.0.0/0"
    gateway_id = aws_internet_gateway.igw.id
  }

  tags = {
    Name        = "devops-project-public-rt"
    Environment = var.environment
    Project     = "devops-project"
    ManagedBy   = "terraform"
    Type        = "public"
  }
}
# Association - Public Subnet #1 WEB - Route Table
resource "aws_route_table_association" "public_web" {
  subnet_id      = aws_subnet.subnet_web.id
  route_table_id = aws_route_table.public_rt.id
}

# Association - Public Subnet #2 ALB - Route Table
resource "aws_route_table_association" "public_alb" {
  subnet_id      = aws_subnet.subnet_alb.id
  route_table_id = aws_route_table.public_rt.id
}

# ============ NAT GATEWAY & ROUTE TABLE ============= #

# Elastic IP for NAT Gateway
resource "aws_eip" "project-eip" {
  domain = "vpc"
}

# NAT Gateway
resource "aws_nat_gateway" "ngw" {
  allocation_id = aws_eip.project-eip.id
  subnet_id     = aws_subnet.subnet_web.id

  tags = {
    Name        = "devops-project-nat"
    Environment = var.environment
    Project     = "devops-project"
    ManagedBy   = "terraform"
  }
}

# Route Table for Private Subnets
resource "aws_route_table" "private_rt" {
  vpc_id = aws_vpc.main.id

  tags = {
    Name        = "devops-project-private-rt"
    Environment = var.environment
    Project     = "devops-project"
    ManagedBy   = "terraform"
    Type        = "private"
  }
}

# Private Route - Private Subnets to NAT Gateway
resource "aws_route" "private_route" {
  route_table_id         = aws_route_table.private_rt.id
  destination_cidr_block = "0.0.0.0/0"
  nat_gateway_id         = aws_nat_gateway.ngw.id
}

# Association - Private Subnet #3 API - Route Table
resource "aws_route_table_association" "Private_1" {
  subnet_id      = aws_subnet.subnet_api.id
  route_table_id = aws_route_table.private_rt.id
}

# Association - Private Subnet #4 DB - Route Table
resource "aws_route_table_association" "Private_2" {
  subnet_id      = aws_subnet.subnet_db.id
  route_table_id = aws_route_table.private_rt.id
}

# ================== SECURITY GROUP ================== #

# Security Group - Connection Access
resource "aws_security_group" "sec_group_vpc" {
  name        = "sec-group-vpc"
  description = "Allow incoming HTTP Connections"
  vpc_id      = aws_vpc.main.id

  # HTTP
  ingress {
    description = "Allow incoming HTTP for redirect to HTTPS"
    from_port   = 80
    to_port     = 80
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }

  # HTTPS
  ingress {
    description = "Allow incoming HTTPS"
    from_port   = 443
    to_port     = 443
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }

  # SSH
  ingress {
    description = "Allow SSH from only our VPC"
    from_port   = 22
    to_port     = 22
    protocol    = "tcp"
    cidr_blocks = [var.vpc_configuration.cidr]
  }

  # Allow all Outbound Traffic
  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }

  tags = {
    Name        = "devops-project-vpc-sg"
    Environment = var.environment
    Project     = "devops-project"
    ManagedBy   = "terraform"
    Type        = "vpc-security"
  }
}

# ==================================================== #
